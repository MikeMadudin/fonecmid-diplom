
#Область ПрограммныйИнтерфейс

// Процедура записывает уведомление в справочник для последующей отправки в Телеграм
//
// Параметры:
//  ТекстСообщения - Строка - Текст уведомления
Процедура ЗаписатьУведомлениеВСправочник(ТекстСообщения) Экспорт

	Попытка
		СпрОбъект = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		СпрОбъект.ТекстСообщения = ТекстСообщения;
		СпрОбъект.Записать();
	Исключение
		ОбщегоНазначения.СообщитьПользователю("Не удалось создать уведомление по причине: "
			+ ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура отправляет боту в Телеграм уведомления
//
Процедура ОтправкаУведомленийПоРасписанию() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка,
		|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
		|ГДЕ
		|	НЕ ВКМ_УведомленияТелеграмБоту.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ВКМ_РаботаСТелеграм.ОтправкаСообщенияВТелеграм(Выборка.ТекстСообщения) Тогда
				// Удаляем сообщение из справочника
				Попытка
					СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
					СправочникОбъект.Удалить();
				Исключение
					ЗаписьЖурналаРегистрации("Ошибка удаления элемента справочника",
											УровеньЖурналаРегистрации.Ошибка
											,
											,
											,
											ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти