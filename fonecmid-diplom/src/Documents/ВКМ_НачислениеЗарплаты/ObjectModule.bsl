
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли;
	
	ПериодРегистрации = НачалоМесяца(ПериодРегистрации);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвиженияПоНачислениям();
	СформироватьДвиженияПоВзаиморасчетам();
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияПоНачислениям()
	
	Для Каждого Строка Из Начисления Цикл
		Если ТипЗнч(Строка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ОсновныеНачисления") Тогда
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = НачалоДня(ПериодРегистрации);
			Движение.ВидРасчета = Строка.ВидРасчета;
			Движение.Сторно = Ложь;
			Движение.ПериодДействияНачало = НачалоДня(Строка.ДатаНачала);
			Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
			Движение.Сотрудник = Строка.Сотрудник;
		КонецЕсли;
	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	Начисления.НомерСтроки КАК НомерСтроки,
				   |	Начисления.Сотрудник КАК Сотрудник,
				   |	Начисления.ВидРасчета КАК ВидРасчета,
				   |	Начисления.РезультатОклад + Начисления.СуммаКОплатеСпециалисту КАК ВыплатаИтог,
				   |	Начисления.ФактДней КАК ФактДней
				   |ПОМЕСТИТЬ вт_НачисленияОклад
				   |ИЗ
				   |	(ВЫБРАТЬ
				   |		ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
				   |		ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
				   |		ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
				   |		ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия КАК НормаДней,
				   |		ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ФактДней,
				   |		ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Оклад,
				   |		ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот, 0) КАК ПроцентОтРабот,
				   |		ОКР(ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) /
				   |			ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия *
				   |			ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 2) КАК РезультатОклад,
				   |		ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот, 0) КАК СуммаКОплатеСпециалисту
				   |	ИЗ
				   |		РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка
				   |		И ПериодРегистрации = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
				   |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&ПериодРегистрации,) КАК
				   |				ВКМ_УсловияОплатыСотрудниковСрезПоследних
				   |			ПО ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
				   |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(НАЧАЛОПЕРИОДА(&ПериодРегистрации,
				   |				МЕСЯЦ), КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), Месяц,) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
				   |			ПО ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник) КАК
				   |		Начисления
				   |СГРУППИРОВАТЬ ПО
				   |	Начисления.НомерСтроки,
				   |	Начисления.Сотрудник,
				   |	Начисления.ВидРасчета,
				   |	Начисления.РезультатОклад + Начисления.СуммаКОплатеСпециалисту,
				   |	Начисления.ФактДней
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	НачисленияЗаГод.Сотрудник КАК Сотрудник,
				   |	СУММА(НачисленияЗаГод.СреднееНачисление) КАК НачисленияЗаГод
				   |ПОМЕСТИТЬ вт_НачисленияЗаГод
				   |ИЗ
				   |	(ВЫБРАТЬ
				   |		ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
				   |		ВКМ_ОсновныеНачисления.Результат КАК СреднееНачисление
				   |	ИЗ
				   |		РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
				   |	ГДЕ
				   |		ВКМ_ОсновныеНачисления.ПериодРегистрации МЕЖДУ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), МЕСЯЦ,
				   |			-12) И КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
				   |		И ВКМ_ОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Оклад)
				   |
				   |	ОБЪЕДИНИТЬ ВСЕ
				   |
				   |	ВЫБРАТЬ
				   |		ВКМ_ДополнительныеНачисления.Сотрудник,
				   |		ВКМ_ДополнительныеНачисления.Результат
				   |	ИЗ
				   |		РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
				   |	ГДЕ
				   |		ВКМ_ДополнительныеНачисления.ПериодРегистрации МЕЖДУ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), МЕСЯЦ,
				   |			-12) И КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)) КАК НачисленияЗаГод
				   |СГРУППИРОВАТЬ ПО
				   |	НачисленияЗаГод.Сотрудник
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
				   |	СУММА(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия) КАК РабочихДнейВГоду
				   |ПОМЕСТИТЬ вт_РабочиеДниВГоду
				   |ИЗ
				   |	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(ПериодРегистрации
				   |		МЕЖДУ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), МЕСЯЦ, -12) И КОНЕЦПЕРИОДА(&ПериодРегистрации,
				   |		МЕСЯЦ)) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
				   |СГРУППИРОВАТЬ ПО
				   |	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	вт_НачисленияОклад.НомерСтроки КАК НомерСтроки,
				   |	вт_НачисленияОклад.Сотрудник КАК Сотрудник,
				   |	вт_НачисленияОклад.ВидРасчета КАК ВидРасчета,
				   |	ОКР(ВЫБОР
				   |		КОГДА вт_НачисленияОклад.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Оклад)
				   |			ТОГДА вт_НачисленияОклад.ВыплатаИтог
				   |		ИНАЧЕ вт_НачисленияЗаГод.НачисленияЗаГод / вт_РабочиеДниВГоду.РабочихДнейВГоду * вт_НачисленияОклад.ФактДней
				   |	КОНЕЦ, 2) КАК ВыплатаИтог
				   |ИЗ
				   |	вт_НачисленияОклад КАК вт_НачисленияОклад
				   |		ЛЕВОЕ СОЕДИНЕНИЕ вт_НачисленияЗаГод КАК вт_НачисленияЗаГод
				   |		ПО вт_НачисленияОклад.Сотрудник = вт_НачисленияЗаГод.Сотрудник
				   |		ЛЕВОЕ СОЕДИНЕНИЕ вт_РабочиеДниВГоду КАК вт_РабочиеДниВГоду
				   |		ПО вт_НачисленияОклад.Сотрудник = вт_РабочиеДниВГоду.Сотрудник";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоДня(ПериодРегистрации));

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		Движение.Результат = Выборка.ВыплатаИтог;

		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = НачалоДня(ПериодРегистрации);
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Результат = Выборка.ВыплатаИтог - (Выборка.ВыплатаИтог * 0.87);
	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать();

КонецПроцедуры

Процедура СформироватьДвиженияПоВзаиморасчетам()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник
				   |ПОМЕСТИТЬ вт_Сотрудники
				   |ИЗ
				   |	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
				   |ГДЕ
				   |	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВКМ_ОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
				   |	СУММА(ВКМ_ОсновныеНачисления.Результат) КАК СуммаОсновныхНачислений
				   |ПОМЕСТИТЬ вт_ОсновныеНачисления
				   |ИЗ
				   |	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
				   |ГДЕ
				   |	ВКМ_ОсновныеНачисления.Сотрудник В
				   |		(ВЫБРАТЬ
				   |			вт_Сотрудники.Сотрудник КАК Сотрудник
				   |		ИЗ
				   |			вт_Сотрудники КАК вт_Сотрудники)
				   |	И ВКМ_ОсновныеНачисления.ПериодРегистрации = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
				   |	И ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
				   |СГРУППИРОВАТЬ ПО
				   |	ВКМ_ОсновныеНачисления.ПериодРегистрации,
				   |	ВКМ_ОсновныеНачисления.Сотрудник
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВКМ_ДополнительныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
				   |	СУММА(ВКМ_ДополнительныеНачисления.Результат) КАК СуммаДополнительныхНачислений
				   |ПОМЕСТИТЬ вт_ДопНачисления
				   |ИЗ
				   |	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
				   |ГДЕ
				   |	ВКМ_ДополнительныеНачисления.Сотрудник В
				   |		(ВЫБРАТЬ
				   |			вт_Сотрудники.Сотрудник КАК Сотрудник
				   |		ИЗ
				   |			вт_Сотрудники КАК вт_Сотрудники)
				   |	И ВКМ_ДополнительныеНачисления.ПериодРегистрации = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
				   |	И ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
				   |СГРУППИРОВАТЬ ПО
				   |	ВКМ_ДополнительныеНачисления.ПериодРегистрации,
				   |	ВКМ_ДополнительныеНачисления.Сотрудник
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВКМ_Удержания.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВКМ_Удержания.Сотрудник КАК Сотрудник,
				   |	СУММА(ВКМ_Удержания.Результат) КАК СуммаУдержаний
				   |ПОМЕСТИТЬ вт_Удержания
				   |ИЗ
				   |	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
				   |ГДЕ
				   |	ВКМ_Удержания.Сотрудник В
				   |		(ВЫБРАТЬ
				   |			вт_Сотрудники.Сотрудник КАК Сотрудник
				   |		ИЗ
				   |			вт_Сотрудники КАК вт_Сотрудники)
				   |	И ВКМ_Удержания.ПериодРегистрации = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
				   |	И ВКМ_Удержания.Регистратор = &Ссылка
				   |СГРУППИРОВАТЬ ПО
				   |	ВКМ_Удержания.ПериодРегистрации,
				   |	ВКМ_Удержания.Сотрудник
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	вт_ОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
				   |	вт_ОсновныеНачисления.Сотрудник КАК Сотрудник,
				   |	вт_ОсновныеНачисления.СуммаОсновныхНачислений КАК СуммаОсновныхНачислений,
				   |	ЕСТЬNULL(вт_ДопНачисления.СуммаДополнительныхНачислений, 0) КАК СуммаДополнительныхНачислений,
				   |	ЕСТЬNULL(вт_Удержания.СуммаУдержаний, 0) КАК СуммаУдержаний,
				   |	вт_ОсновныеНачисления.СуммаОсновныхНачислений + ЕСТЬNULL(вт_ДопНачисления.СуммаДополнительныхНачислений, 0) -
				   |		ЕСТЬNULL(вт_Удержания.СуммаУдержаний, 0) КАК СуммаРезультат
				   |ИЗ
				   |	вт_ОсновныеНачисления КАК вт_ОсновныеНачисления
				   |		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДопНачисления КАК вт_ДопНачисления
				   |		ПО вт_ОсновныеНачисления.Сотрудник = вт_ДопНачисления.Сотрудник
				   |		ЛЕВОЕ СОЕДИНЕНИЕ вт_Удержания КАК вт_Удержания
				   |		ПО вт_ОсновныеНачисления.Сотрудник = вт_Удержания.Сотрудник";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоДня(ПериодРегистрации));

	Выборка = Запрос.Выполнить().Выбрать();
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.СуммаРезультат;
		Движение.Период = Выборка.ПериодРегистрации;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли