#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли;
	
	ПериодРегистрации = НачалоМесяца(ПериодРегистрации);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ,Режим)
	
	СформироватьДвиженияПоНачислениям();
	СформироватьДвиженияПоВзаиморасчетам();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияПоНачислениям()
	
	Для Каждого Строка Из СписокСотрудников Цикл
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ПремияФикс;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Результат = Строка.СуммаПремии;

		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Результат = Строка.СуммаПремии * 13 / 100;
	КонецЦикла;

	Движения.ВКМ_ДополнительныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура СформироватьДвиженияПоВзаиморасчетам()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Сотрудник КАК Сотрудник
				   |ПОМЕСТИТЬ вт_Сотрудники
				   |ИЗ
				   |	Документ.ВКМ_НачислениеФиксированнойПремии.СписокСотрудников КАК ВКМ_НачислениеФиксированнойПремииСписокСотрудников
				   |ГДЕ
				   |	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Ссылка = &Ссылка
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВКМ_ДополнительныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
				   |	СУММА(ВКМ_ДополнительныеНачисления.Результат) КАК СуммаДополнительныхНачислений
				   |ПОМЕСТИТЬ вт_ДопНачисления
				   |ИЗ
				   |	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
				   |ГДЕ
				   |	ВКМ_ДополнительныеНачисления.Сотрудник В
				   |			(ВЫБРАТЬ
				   |				вт_Сотрудники.Сотрудник КАК Сотрудник
				   |			ИЗ
				   |				вт_Сотрудники КАК вт_Сотрудники)
				   |	И ВКМ_ДополнительныеНачисления.ПериодРегистрации = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
				   |	И ВКМ_ДополнительныеНачисления.Регистратор = &ссылка
				   |
				   |СГРУППИРОВАТЬ ПО
				   |	ВКМ_ДополнительныеНачисления.ПериодРегистрации,
				   |	ВКМ_ДополнительныеНачисления.Сотрудник
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВКМ_Удержания.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВКМ_Удержания.Сотрудник КАК Сотрудник,
				   |	СУММА(ВКМ_Удержания.Результат) КАК СуммаУдержаний
				   |ПОМЕСТИТЬ вт_Удержания
				   |ИЗ
				   |	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
				   |ГДЕ
				   |	ВКМ_Удержания.Сотрудник В
				   |			(ВЫБРАТЬ
				   |				вт_Сотрудники.Сотрудник КАК Сотрудник
				   |			ИЗ
				   |				вт_Сотрудники КАК вт_Сотрудники)
				   |	И ВКМ_Удержания.ПериодРегистрации = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
				   |	И ВКМ_Удержания.Регистратор = &ссылка
				   |
				   |СГРУППИРОВАТЬ ПО
				   |	ВКМ_Удержания.ПериодРегистрации,
				   |	ВКМ_Удержания.Сотрудник
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	вт_ДопНачисления.ПериодРегистрации КАК ПериодРегистрации,
				   |	вт_ДопНачисления.Сотрудник КАК Сотрудник,
				   |	вт_ДопНачисления.СуммаДополнительныхНачислений КАК СуммаДополнительныхНачислений,
				   |	вт_Удержания.СуммаУдержаний КАК СуммаУдержаний,
				   |	вт_ДопНачисления.СуммаДополнительныхНачислений - вт_Удержания.СуммаУдержаний КАК СуммаРезультат
				   |ИЗ
				   |	вт_ДопНачисления КАК вт_ДопНачисления
				   |		ЛЕВОЕ СОЕДИНЕНИЕ вт_Удержания КАК вт_Удержания
				   |		ПО вт_ДопНачисления.Сотрудник = вт_Удержания.Сотрудник";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоДня(ПериодРегистрации));

	Выборка = Запрос.Выполнить().Выбрать();
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.СуммаРезультат;
		Движение.Период = Выборка.ПериодРегистрации;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли