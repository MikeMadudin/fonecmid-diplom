
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АдресВХ") Тогда
		АдресХранилища = Параметры.АдресВХ;
		Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
			СтруктураПараметров = ПолучитьИзВременногоХранилища(АдресХранилища);
			ЗаполнитьДиаграмму(СтруктураПараметров, Диаграмма);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДиаграмму(СтруктураПараметров, Диаграмма)
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();

	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	Диаграмма.УстановитьПолныйИнтервал(НачалоГода(Дата(СтруктураПараметров.Год, 1, 1)), КонецГода(Дата(
		СтруктураПараметров.Год, 1, 1)));

	Серия = Диаграмма.УстановитьСерию("Отпуска сотрудников");
	Для Каждого ОтпускСотрудника Из СтруктураПараметров.ОтпускаСотрудников Цикл
		Точка = Диаграмма.УстановитьТочку(ОтпускСотрудника.Сотрудник);
		Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
		Интервал = Значение.Добавить();
		Интервал.Начало = ОтпускСотрудника.ДатаНачала;
		Интервал.Конец = ОтпускСотрудника.ДатаОкончания;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти