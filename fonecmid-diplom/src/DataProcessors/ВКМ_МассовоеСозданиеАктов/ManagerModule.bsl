#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция создает документы Реализация товаров и услуг пакетом
//
// Возвращаемое значение:
// Булево
Функция СоздатьРеализацииПакетом(Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ДоговорыКонтрагентов.Ссылка КАК Договор,
				   |	ДоговорыКонтрагентов.Организация КАК Организация,
				   |	ДоговорыКонтрагентов.Владелец КАК Контрагент
				   |ПОМЕСТИТЬ вт_ДоговорыЗаПериод
				   |ИЗ
				   |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
				   |ГДЕ
				   |	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
				   |	И ДоговорыКонтрагентов.ВКМ_ПериодДействияНачало <= НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
				   |	И ДоговорыКонтрагентов.ВКМ_ПериодДействияКонец >= КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	Реализации.Договор КАК Договор,
				   |	Реализации.Организация КАК Организация,
				   |	Реализации.Контрагент КАК Контрагент,
				   |	Реализации.Реализация КАК Реализация
				   |ИЗ
				   |	(ВЫБРАТЬ
				   |		вт_ДоговорыЗаПериод.Договор КАК Договор,
				   |		РеализацияТоваровУслуг.Ссылка КАК Реализация,
				   |		вт_ДоговорыЗаПериод.Организация КАК Организация,
				   |		вт_ДоговорыЗаПериод.Контрагент КАК Контрагент
				   |	ИЗ
				   |		вт_ДоговорыЗаПериод КАК вт_ДоговорыЗаПериод
				   |			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
				   |			ПО вт_ДоговорыЗаПериод.Договор = РеализацияТоваровУслуг.Договор) КАК Реализации
				   |ГДЕ
				   |	Реализации.Реализация ЕСТЬ NULL";

	Запрос.УстановитьПараметр("Период", Период);

	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		СозданныеРеализации = Новый Массив;
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяРеализация = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			
			ДанныеЗаполнения = НовыеДанныеЗаполнения(Выборка);
			НоваяРеализация.Заполнить(ДанныеЗаполнения);
			НоваяРеализация.ВыполнитьАвтозаполнение();
			
			Попытка
				НоваяРеализация.Записать(РежимЗаписиДокумента.Проведение);
				СозданныеРеализации.Добавить(НоваяРеализация.Ссылка);
			Исключение
				ЗаписьЖурналаРегистрации("Ошибка проведения реализации", УровеньЖурналаРегистрации.Ошибка, НоваяРеализация, ,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			
		КонецЦикла;
		
		Возврат СозданныеРеализации;
		
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеДанныеЗаполнения(Выборка)

	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Договор", Выборка.Договор);
	ДанныеЗаполнения.Вставить("Организация", Выборка.Организация);
	ДанныеЗаполнения.Вставить("Контрагент", Выборка.Контрагент);
	ДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());

	Возврат ДанныеЗаполнения;

КонецФункции

#КонецОбласти

#КонецЕсли